
    // if over this many days, it's not treated as a readmission
    var readmissionDaysLimit = 28

    return CALCULATE (
        // find the number of ward stays
        COUNTROWS( 'dbth WardStay'),
        // taking into account only disrcharge wardstays (last wardstay in spell)
        'dbth WardStay'[IsLastStayInSpell],
        // such that there is one or more spells that started for the same patient within the designated window of time
        FILTER (
            'dbth Spell',
            COUNTROWS (
                CALCULATETABLE (
                    'dbth Spell',
                    REMOVEFILTERS(), // remove any filters from visuals such as breakdown by ward, the readmission doesn't necessarily have to be into the same ward
                    FILTER (
                        'dbth Spell',
                        'dbth Spell'[PatientIdHash] = EARLIER ( 'dbth Spell'[PatientIdHash] )
                            && 'dbth Spell'[Admitted] > EARLIER('dbth Spell'[Discharged])
                            && ABS(datediff('dbth Spell'[Admitted], EARLIER('dbth Spell'[Discharged]), day)) <= readmissionDaysLimit
                    )
                )
            ) > 0
        ),
        -- using discharge date for date filter
        USERELATIONSHIP('dbth WardStay'[DateEnd], Dates[Date])
    )